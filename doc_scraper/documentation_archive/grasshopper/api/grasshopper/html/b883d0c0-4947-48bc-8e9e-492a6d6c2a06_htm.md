---
url: https://developer.rhino3d.com/api/grasshopper/html/b883d0c0-4947-48bc-8e9e-492a6d6c2a06.htm#GH_ComponentSolveInstance
scraped_at: 2025-09-08T16:25:14.527915
title: Untitled
---

Grasshopper API

[Grasshopper API](../html/723c01da-9986-4db2-8f53-6f3a7494df75.htm
"Grasshopper API")

[Examples](../html/d113a9f0-6e27-46df-8316-2079c44382ac.htm "Examples")

[Visual Basic .NET](../html/b883d0c0-4947-48bc-8e9e-492a6d6c2a06.htm "Visual
Basic .NET")

[Simple Component (VB)](../html/b883d0c0-4947-48bc-8e9e-492a6d6c2a06.htm
"Simple Component \(VB\)")

[Simple Mathematics (VB)](../html/2824c770-2673-49a3-8683-1a70bc0349cc.htm
"Simple Mathematics \(VB\)")

[Simple Geometry (VB)](../html/4306b177-1bf1-41bc-ac0e-2f6869d02365.htm
"Simple Geometry \(VB\)")

[Simple Data Types (VB)](../html/f9aa207f-3d19-414c-af01-1e5ad42a8cab.htm
"Simple Data Types \(VB\)")

[Simple Parameters (VB)](../html/0edd8dc9-32a7-40aa-b217-8e01e35e58bc.htm
"Simple Parameters \(VB\)")

[List Component (VB)](../html/4db493ec-0bb3-4b73-943a-fdff03863e1d.htm "List
Component \(VB\)")

[Extending the GUI (VB)](../html/99cd32c8-7c1f-4f9a-87ea-76b032de7f70.htm
"Extending the GUI \(VB\)")

[Custom Attributes (VB)](../html/ad6e93fe-e1c8-451e-a6d0-77cb8dd4516d.htm
"Custom Attributes \(VB\)")

[Custom Component Options
(VB)](../html/434018c0-6110-4478-bf2a-dcd099d8b8b2.htm "Custom Component
Options \(VB\)")

![Click or drag to resize](../icons/TocOpen.gif)![Click or drag to
resize](../icons/TocClose.gif)

# Simple Component (VB)  
  
---  
  
  * Introduction
  * Grasshopper.Kernel.GH_Component
  * The Component Constructor
  * Component GUIDs
  * Parameter Registration
  * The Solver Routine

This article contains an exhaustive, step by step explanation of how to build
your first component in VB.NET. It will skip over any complicated issues (such
as mathematics, geometry and data handling) in order to reduce the totality of
new concepts. You will however need to have a good understanding of basic OOP
concepts such as classes, types and inheritance. If you do not understand
these DotNET essentials, we recommend you start with some other reading
material first.

![](../icons/SectionExpanded.png)Grasshopper.Kernel.GH_Component

All you need to do in order to define a new Component is to inherit from the
[GH_Component](T_Grasshopper_Kernel_GH_Component.htm) base class in
Grasshopper. Assuming you've set up your project correctly, create a new,
blank class in your project:

![](../media/VB_CreateNewClass.png)

At this point a new file should be created (MyFirstComponent.vb) with the
following content:

VB

Copy

    
    
    Public Class MyFirstComponent
    
    End Class

Since we'll be using primarily types from the Grasshopper.Kernel namespace.
We'll import this namespace so that we have easy access to all types contained
within it (unless otherwise specified, all further types discussed in this
topic belong to Grasshopper.Kernel):

VB

Copy

    
    
    Imports Grasshopper.Kernel
    Public Class MyFirstComponent
    
    End Class

Now, we need to derive our MyFirstComponent class from the GH_Component base
class defined inside Grasshopper. GH_Component takes care of almost all the
complicated actions and mannerisms that constitute a Component. It will handle
data conversion, GUI, menus, file Input/Output, Error trapping and much, much
more. This allows us to focus only on the unique aspects of our Component. In
order to derive from GH_Component, add the following line directly underneath
the class declarator:

VB

Copy

    
    
    Imports Grasshopper.Kernel
    Public Class MyFirstComponent
      Inherits GH_Component
    
    End Class

As soon as you press Enter after finishing that line, Visual Studio will
insert a bunch of empty properties and subroutines which we are required to
fill out, but we'll deal with that in the next sections:

VB

Copy

    
    
    Imports Grasshopper.Kernel
    Public Class MyFirstComponent
      Inherits GH_Component
    
      Protected Overrides Sub RegisterInputParams(ByVal pManager As GH_Component.GH_InputParamManager)
        'Don't worry about this just yet!
      End Sub
    
      Protected Overrides Sub RegisterOutputParams(ByVal pManager As GH_Component.GH_OutputParamManager)
        'We'll get to it soon enough.
      End Sub
    
      Protected Overrides Sub SolveInstance(ByVal DA As IGH_DataAccess)
        'I know it all looks scary.
      End Sub
    
      Public Overrides ReadOnly Property ComponentGuid() As System.Guid
        Get
          'But we'll deal with it one item at a time.  
        End Get
      End Property
    End Class

![](../icons/SectionExpanded.png)The Component Constructor

As we've seen in the previous section, Visual Studio will populate the
MyFirstComponent class with a collection of properties and subroutines that we
need to implement. There is however another subroutine that requires our
attention that is missing. This is the _constructor_. The constructor is a
special subroutine inside each class which gets called when the class is
instantiated (or "constructed"). This can happen only once (we feeble humans
can only be born once as well after all) and it necessarily happens before
anything else is allowed to happen. The GH_Component  base class has a
constructor which is not empty, so we have to call that constructor from
within our constructor and supply it with all the information it needs. Let's
play ball. Add the following code near the top of the MyFirstComponent class:

VB

Copy

    
    
    Imports Grasshopper.Kernel
    Public Class MyFirstComponent
      Inherits GH_Component
    
      Public Sub New()
        MyBase.New("MyFirst", "MFC", "My first component", "Extra", "Simple")
      End Sub
    
      ...

As you can see we need to supply a set of text constants, which are used to
name and identify our component within the Grasshopper GUI. The text fields
are:

Parameter| Purpose  
---|---  
name| The name of our component. The name is what appears on tooltips and
Panel dropdowns.  
abbreviation| The abbreviation of our component. The abbreviation is what is
written on the component once it appears on the Canvas.  
description| A description of our component. The description is used on
tooltips to provide users with a more detailed idea about what this component
is for.  
category| The tab category for the component. The category equals the name of
the Tab onto which the Component will appear. If a non-existing category is
supplied, a new Tab will be added to the Grasshopper GUI.  
subCategory| The panel category for the component. The sub-category equals the
name of the Panel onto which the Component will appear. If a non-existing sub-
category is supplied, a new Panel will be added to the Category Tab.  
  
![](../media/Component_TextConstants.png)

![](../icons/SectionExpanded.png)Component Guids

Every type of object inside a Grasshopper document must have a Guid associated
with it. When a Grasshopper file (*.gh or *.ghx) is written these Guids are
used as markers, so it becomes clear what portions of the file belong to which
object. When the file is read back in, that marker is compared against the
list of all cached components and if a match is found the appropriate
component is asked to please go and deserialize itself. (You thought I was
going to say something else didn't you?). When no matching component can be
found it is assumed that whoever wrote the file had access to certain
components that are not available locally, and that portion of the file is
dutifully skipped.

So, long story short, we need to invent a Guid (Globally Unique IDentifier)
that will positively and unerringly indicate _this_ component. You can
generate spanking new Guids using an [Online Guid
Generator](http://www.newguid.com/) or Microsofts popular guidgen.exe.
**Never** re-use a Guid and **never** edit one by hand. Always generate a
proper one using an official tool.

Once you have a new Guid standing by, modify the
[ComponentGuid](P_Grasshopper_Kernel_IGH_DocumentObject_ComponentGuid.htm)
property to return it:

VB

Copy

    
    
    ...
    Public Overrides ReadOnly Property ComponentGuid() As System.Guid
      Get
        'Don't copy this GUID, make a new one
        Return New Guid("419c3a3a-cc48-4717-8cef-5f5647a5ecfc")  
      End Get
    End Property
    ...

![](../icons/SectionExpanded.png)Parameter Registration

Components have unique input and output parameters which are most often fixed.
We are ignoring those rare cases where a component either has no inputs or no
outputs, or where there is a variable number of parameters. There are two
subroutines that allow you to define (or "register") these parameters. These
routines are called from within the base class constructor and they are only
called once. Let's have a look at the default implementation that Visual
Studio generated again:

VB

Copy

    
    
    ...
    Protected Overrides Sub RegisterInputParams(ByVal pManager As GH_Component.GH_InputParamManager)
    
    End Sub
    
    Protected Overrides Sub RegisterOutputParams(ByVal pManager As GH_Component.GH_OutputParamManager)
    
    End Sub
    ...

Although it would technically be possible to manually register parameters, we
_highly_ recommend you use the methods on pManager. pManager has methods for
adding all the basic parameter types and it often even allows you to specify
default values:

![](../media/VB_PManagerAutoComplete.png)

In this example we'll only create two parameters (one input, one output) and
they will both be of type String.

VB

Copy

    
    
    ...
    Protected Overrides Sub RegisterInputParams(ByVal pManager As Grasshopper.Kernel.GH_Component.GH_InputParamManager)
      pManager.AddTextParameter("String", "S", "String to reverse")
    End Sub
    
    Protected Overrides Sub RegisterOutputParams(ByVal pManager As Grasshopper.Kernel.GH_Component.GH_OutputParamManager)
      pManager.AddTextParameter("Reverse", "R", "Reversed string")
    End Sub
    ...

When we compile this project (assuming it has been setup correctly), the
component will already be available on the Grasshopper tabs and it can be
placed onto the canvas:

![](../media/MyFirstComponentOnCanvas.png)

![](../icons/SectionExpanded.png)The Solver Routine

Our new component sure looks perky and expensive, but it doesn't do anything
useful yet (am I the only one who's reminded of Paris Hilton?). We still need
to write the contents of the
[SolveInstance](M_Grasshopper_Kernel_GH_Component_SolveInstance.htm)
subroutine, which is where all the action takes place. The SolveInstance()
function is called upon whenever the component needs to handle input data. In
this particular example, if we plug a list of twelve Strings into the [S]
parameter, SolveInstance() will be called twelve times.

As you may already have guessed, the component we're writing will reverse a
given textual string from [S] and output the result to [R]. Since we're
operating on individual items of data (the default behaviour), all we need to
do inside the SolveInstance() function is retrieve the current String from
[S], reverse it and assign it to [R]. Now, String reversal is not a function
that is directly available in the framework String type, so we need to
actually do some thinking:

VB

Copy

    
    
    ...
    Protected Overrides Sub SolveInstance(ByVal DA As Grasshopper.Kernel.IGH_DataAccess)
      'Declare a variable for the input String
      Dim data As String = Nothing
    
      'Use the DA object to retrieve the data inside the first input parameter.
      'If the retieval fails (for example if there is no data) we need to abort.
      If (Not DA.GetData(0, data)) Then Return
    
      'If the retrieved data is Nothing, we need to abort.
      'We're also going to abort on a zero-length String.
      If (data Is Nothing) Then Return
      If (data.Length = 0) Then Return
    
      'Convert the String to a character array.
      Dim chars As Char() = data.ToCharArray()
    
      'Reverse the array of character.
      Array.Reverse(chars)
    
      'Use the DA object to assign a new String to the first output parameter.
      DA.SetData(0, New String(chars))
    End Sub
    ...

![](../media/MyFirstComponentVictory.png)

Grasshopper for Rhino 8.22.25217.12450 (2025-08-05)  
Copyright Â© 2009-2025 Robert McNeel & Associates

[Report wishes and bugs at
discourse.mcneel.com](https://discourse.mcneel.com/c/grasshopper)

